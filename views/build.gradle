/*
 * Copyright (C) 2021 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 33

    namespace "com.example.android.wearable.wear.alwayson"

    defaultConfig {
        versionCode 1
        versionName "1.0"
        minSdk 26
        targetSdk 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        warningsAsErrors true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        allWarningsAsErrors = true
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }

    testOptions {
        devices {
            squareApi30(com.android.build.api.dsl.ManagedVirtualDevice) {
                device = "Wear OS Square"
                apiLevel = 30
                systemImageSource = "android-wear"
            }
            roundApi28(com.android.build.api.dsl.ManagedVirtualDevice) {
                device = "Wear OS Large Round"
                apiLevel = 28
                systemImageSource = "android-wear"
            }
            roundApi30(com.android.build.api.dsl.ManagedVirtualDevice) {
                device = "Wear OS Large Round"
                apiLevel = 30
                systemImageSource = "android-wear"
            }
        }
    }

    buildFeatures {
        viewBinding true
    }

    // Workaround for https://github.com/Kotlin/kotlinx.coroutines/issues/2023
    packagingOptions {
        // for JNA and JNA-platform
        exclude "META-INF/AL2.0"
        exclude "META-INF/LGPL2.1"
    }
}

dependencies {
    implementation libs.kotlinx.coroutines.android
    implementation libs.androidx.activity.ktx
    implementation libs.androidx.core.ktx
    implementation libs.androidx.wear

    androidTestImplementation libs.kotlinx.coroutines.test
    androidTestImplementation libs.test.core
    androidTestImplementation libs.test.core.ktx
    androidTestImplementation libs.test.espresso.core
    androidTestImplementation libs.test.ext.junit
    androidTestImplementation libs.test.ext.junit.ktx
    androidTestImplementation libs.test.ext.truth
    androidTestImplementation libs.test.runner
    androidTestImplementation libs.test.rules
    androidTestImplementation libs.test.uiautomator
    androidTestImplementation libs.com.google.truth
}
